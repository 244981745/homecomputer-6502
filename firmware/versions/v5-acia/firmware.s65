                  .setcpu "6502"

                  ACIA_DATA = $7f00
                  ACIA_STATUS = $7f01
                  ACIA_COMMAND = $7f02
                  ACIA_CONTROL = $7f03

                  .segment "VECTORS"

                  .word   nmi
                  .word   reset
                  .word   irq

                  .code

reset:            jmp main

nmi:              rti

irq:              rti

main:
init_acia:        lda #%00001011        ;No parity, no echo, no interrupt
                  sta ACIA_COMMAND
                  lda #%00011111        ;1 stop bit, 8 data bits, 19200 baud
                  sta ACIA_CONTROL

write1:           ldx #0
@next_char:       lda hello,x
                  beq read
                  jsr send_char
                  inx
                  jmp @next_char

read:
wait_rxd_full:    lda ACIA_STATUS
                  and #$08
                  beq wait_rxd_full
                  lda ACIA_DATA
                  pha

write2:           ldx #0
@next_char:       lda answer,x
                  beq write3
                  jsr send_char
                  inx
                  jmp @next_char

write3:           pla
                  jsr send_char
                  lda #$0d
                  jsr send_char
                  lda #$0a
                  jsr send_char
                  jmp read

send_char:        pha
@wait_txd_empty:  lda ACIA_STATUS
                  and #$10
                  beq @wait_txd_empty
                  pla
                  sta ACIA_DATA
                  rts

hello:            .byte $0d, $0a, "Hello World!", $0d, $0a, 0
answer:           .byte $0d, $0a, "You typed: ", 0
